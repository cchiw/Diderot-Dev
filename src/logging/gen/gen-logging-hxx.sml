(* gen-logging-hxx.sml
 *
 * This code is part of the Diderot Project (http://diderot-language.cs.uchicago.edu)
 *
 * COPYRIGHT (c) 2016 The University of Chicago
 * All rights reserved.
 *
 * This code was ported from the Manticore project (http://manticore.cs.uchicago.edu)
 *
 * Generate the "logging.hxx" file.
 *)

structure GenLoggingHxx : GENERATOR =
  struct

    structure Sig = EventSig
    structure Map = Sig.Map

    val template = "logging_hxx.in"
    val path = "src/lib/include/diderot/logging.hxx"

  (* generate the inline logging function for a given signature *)
    fun genForSig outS (sign, {isSource, args}) = let
	  fun pr s = TextIO.output(outS, s)
	  fun prl l = TextIO.output(outS, concat l)
	(* generate params for the event arguments *)
	  fun genParams ([], _)= ()
	    | genParams ((_, ty)::r, i) = let
		fun next cty = (
		      prl [", ", cty, "a", Int.toString i];
		      genParams (r, i+1))
		in
		  case ty
		   of Sig.SELF => prl [", uint32_t self"]
		    | Sig.STRAND => next "uint32_t"
		    | Sig.INT => next "int32_t "
		    | Sig.WORD => next "uint32_t "
		    | Sig.FLOAT => next "float "
		    | Sig.DOUBLE => next "double "
		    | Sig.EVENT_ID => next "uint64_t "
		    | Sig.NEW_ID => (* this value is generated by logging function *)
			genParams (r, i+1)
		    | Sig.STR _ => next "const char *"
		  (* end case *)
		end
	(* generate code to copy the event arguments into the event structure *)
	  fun genCopy ([], _) = ()
	    | genCopy ((loc, ty)::r, i) = let
(* FIXME: this code is broken, except w.r.t. "self" arguments *)
		val param = "a" ^ Int.toString i
		val loc = loc - Sig.argStart
		val index = Word.fmt StringCvt.DEC (Word.>>(loc, 0w2))
		in
		  pr "    ";
		  case ty
		   of Sig.SELF => prl["ep->_strand = self;\n"]
		    | Sig.STRAND => prl[
			  "*((uint32_t *)&ep->_data[", index, "]) = ", param, ";\n"
			]
		    | Sig.INT => prl["ep->_data[", index, "] = (uint32_t)", param, ";\n"]
		    | Sig.WORD => prl["ep->_data[", index, "] = ", param, ";\n"]
		    | Sig.FLOAT => prl[
			  "*((float *)&ep->_data[", index, "]) = ", param, ";\n"
			]
		    | Sig.DOUBLE => prl[
			  "*((double *)&ep->_data[", index, "]) = ", param, ";\n"
			]
		    | Sig.EVENT_ID => prl[
			  "*((uint64_t *)&ep->_data[", index, "]) = ", param, ";\n"
			]
		    | Sig.NEW_ID => prl[
			  "uint64_t newId = NewEventId(wrld);\n",
			  "    *((uint64_t *)&ep->_data[", index, "]) = newId;\n"
			]
		    | Sig.STR n => prl[
			  "memcpy (((char *)(ep->_data)) + ", Word.fmt StringCvt.DEC loc, ", ",
			  param, ", ", Int.toString n, ");\n"
			]
		  (* end case *);
		  genCopy (r, i+1)
		end
	  in
	    prl [
		"inline ",
		if isSource then "uint64_t" else "void",
		" _log_event", sign, " (struct diderot::world_base *wrld, uint32_t id, uint32_t evt"
	      ];
	    genParams (args, 0);
	    pr "\
	      \)\n\
	      \{\n\
	      \    diderot::log::file *fp = wrld->_log_file;\n\
	      \    diderot::log::event *ep = fp->next_event(id);\n\
	      \\n\
	      \    set_time_stamp (&(ep->_ts));\n\
	      \    ep->_event = evt;\n";
	    genCopy (args, 0);
	    if isSource
	      then pr "    return newId;\n"
	      else ();
	    pr "\n}\n\n"
	  end

  (* generate an event-specific logging macro *)
    fun genLogMacro outS (LoadFile.EVT{id=0, ...}) = ()
      | genLogMacro outS (LoadFile.EVT ed) = let
	  fun pr s = TextIO.output(outS, s)
	  fun prl l = TextIO.output(outS, concat l)
	  fun prParams [] = ()
	    | prParams ((a : Sig.arg_desc)::r) = (prl [",", #name a]; prParams r)
	  fun prArgs [] = ()
	    | prArgs ((a : Sig.arg_desc)::r) = (prl [", (", #name a, ")"]; prArgs r)
	(* filter out any new-id arguments *)
	  val args = List.filter (not o Sig.isNewIdArg) (#args ed)
	  in
	    prl ["#define Log", #name ed, "(wrld,id"];
	    prParams args;
	    prl [") _log_event", #sign ed, " ((wrld), (id), diderot::log::", #name ed, "Evt"];
	    prArgs (Sig.sortArgs args); (* NOTE: location order here! *)
	    pr ")\n"
	  end

  (* generate a dummy logging macro for when logging is disabled *)
    fun genDummyLogMacro outS (LoadFile.EVT{id=0, ...}) = ()
      | genDummyLogMacro outS (LoadFile.EVT ed) = let
	  fun pr s = TextIO.output(outS, s)
	  fun prl l = TextIO.output(outS, concat l)
	  fun prParams [] = ()
	    | prParams ((a : Sig.arg_desc)::r) = (prl [",", #name a]; prParams r)
	  fun prArgs [] = ()
	    | prArgs ((a : Sig.arg_desc)::r) = (prl [", (", #name a, ")"]; prArgs r)
	(* filter out any new-id arguments *)
	  val args = List.filter (not o Sig.isNewIdArg) (#args ed)
	  in
	    prl ["#define Log", #name ed, "(wrld"];
	    prParams args;
	    pr ") 0\n"
	  end

  (* compute a mapping from signatures to their argument info from the list of event
   * descriptors.
   *)
    fun computeSigMap logDesc = let
	  val isSourceEvt = LoadFile.isSource
	  fun doEvent (evt as LoadFile.EVT{sign, args, ...}, map) = (case Map.find(map, sign)
		 of SOME _ => map
		  | NONE => let
		      val argInfo = {
			      isSource = isSourceEvt evt,
			      args = List.map (fn {loc, ty, ...} => (loc, ty)) args
			    }
		      in
			Map.insert (map, sign, argInfo)
		      end
		(* end case *))
	  in
	    LoadFile.foldEvents doEvent Map.empty logDesc
	  end

    fun hooks (outS, logDesc : LoadFile.log_file_desc) = let
	  val sigMap = computeSigMap logDesc
	  fun genericLogFuns () = Map.appi (genForSig outS) sigMap
	  fun logFunctions () = LoadFile.applyToEvents (genLogMacro outS) logDesc
	  fun dummyLogFunctions () = LoadFile.applyToEvents (genDummyLogMacro outS) logDesc
	  in [
	    ("GENERIC-LOG-FUNCTIONS", genericLogFuns),
	    ("LOG-FUNCTIONS", logFunctions),
	    ("DUMMY-LOG-FUNCTIONS", dummyLogFunctions)
	  ] end

  end
